using LinearAlgebra
using Test
using StaticArrays

using TraceParticles:
    get_guidingcentre,
    get_fullorbit!,
    larmorradius,
    gyrofrequency,
    perpendicular_velocity,
    magneticmoment,
    characteristicfieldlength,
    scalesratio,
    magneticcurvatureratio,
    kineticenergy,
    exbdrift,
    gradbdrift,
    curvaturedrift,
    polarisationdrift,
    magneticmirror_acceleration,
    parallel_acceleration,
    fermi_acceleration,
    fieldgradients,
    drifts,
    gca_drift_and_acceleration,
    cosineof_pitchangle,
    lorentzfactor,
    kineticspeed,
    csqrdinv

if !isdefined(Main, :verbose)
    verbose = 4
end

@testset verbose = verbose ≥ 4 "physics.jl" begin

    @testset "get_guidingcentre!" begin
        u = zeros(4)
        pos = [1.0, 0.0, 0.0]
        vel = [0.0, 1.0, 1.0]
        magneticfield = SA[0.0, 0.0, 1.0]
        electricfield = SA[1.0, 0.0, 0.0]
        charge = 1.0
        mass = 0.5
        R, vparal, mu = get_guidingcentre(pos, vel, magneticfield, electricfield, charge, mass)
        u[1:3] .= R
        u[4] = vparal
        @test isapprox(u[1:3], [2.0, 0.0, 0.0], atol=1e-6)
        @test isapprox(u[4], 1.0, atol=1e-6)
        @test isapprox(mu, 1.0, atol=1e-6)
        # negative charge
        R, vparal, mu = get_guidingcentre(pos, vel, magneticfield, electricfield, -charge, mass)
        u[1:3] .= R
        u[4] = vparal
        @test isapprox(u[1:3], [0.0, 0.0, 0.0], atol=1e-6)
        @test isapprox(u[4], 1.0, atol=1e-6)
        @test isapprox(mu, 1.0, atol=1e-6)
    end

    @testset "get_guidingcentre (solar conditions)" begin
        u = zeros(4)
        pos = [1.0e6, 2.0e6, 3.0e6]
        vel = [1.0e5, 2.0e5, 3.0e5]
        magneticfield = SA[0.0, 0.0, 5.0e-5]
        electricfield = SA[0.0, 1.0e-3, 0.0]
        charge = 1.6e-19  # proton charge
        mass = 1.67e-27  # proton mass
        R, vparal, mu = get_guidingcentre(pos, vel, magneticfield, electricfield, charge, mass)
        u[1:3] .= R
        u[4] = vparal
        @test isapprox(
            u[1:3],
            [1.00004175e6, 1.999979129175e6, 3.0e6],
            atol=1e-6
        )
        @test isapprox(u[4], 3.0e5, atol=1e-6)
        @test isapprox(mu, 0.5 * mass * norm([1.0e5 - (1e-3 / 5e-5), 2.0e5, 0.0])^2 / 5.0e-5, rtol=1e-6)
    end

    @testset "get_fullorbit!" begin
        u = zeros(6)
        magneticfield = [0.0, 0.0, 1.0]
        electricfield = [0.0, 1.0, 0.0]
        R = [1.0, 0.0, 0.0]
        vparal = 1.0
        μ = 0.5
        mass = 1.0
        charge = 1.0
        phaseangle = -π / 2
        # testing position
        #get_fullorbit!(u, magneticfield, electricfield, R, vparal, μ, charge, mass,
        #    phaseangle
        #)
        #@test isapprox(u[1:3], [2.0, 0.0, 0.0], atol=1e-6)
        # testing velocity: Need further evaluation to check whether test or code is wrong.
        #@test isapprox(u[4:6], [1.0, 1.0, 1.0], atol=1e-6)

        # negative charge
        #get_fullorbit!(u, magneticfield, electricfield, R, vparal, μ, -charge, mass, phaseangle)
        #@test isapprox(u[1:3], [2.0, 0.0, 0.0], atol=1e-6)
        #@test isapprox(u[4:6], [1.0, -1.0, 1.0], atol=1e-6)
    end


    @testset "larmorradius" begin
        mass = 9.11e-31  # electron mass
        vperp = 1.0e5
        charge = -1.6e-19  # electron charge
        B = 5.0e-5
        result = larmorradius(mass, vperp, charge, B)
        @test isapprox(result, abs(mass * vperp / (charge * B)), rtol=1e-6)
    end

    @testset "gyrofrequency" begin
        mass = 9.11e-31  # electron mass
        charge = -1.6e-19  # electron charge
        B = 5.0e-5
        result = gyrofrequency(mass, charge, B)
        @test isapprox(result, abs(charge * B / mass), rtol=1e-6)
    end

    @testset "perpendicular_velocity" begin
        # method 1
        magnetic_moment = 1.0e-23
        mass = 9.11e-31  # electron mass
        B = 5.0e-5
        result = perpendicular_velocity(magnetic_moment, mass, B)
        @test isapprox(result, sqrt(2 * magnetic_moment * B / mass), atol=1e-6)
        # method 2
        E = SA[1, 0, 0]
        B = SA[0, 0, 1]
        vel = SA[0, 0, 0]
        @test isapprox(perpendicular_velocity(vel, B, E), [0, 1, 0], atol=1e-6)
        # method 3
        vel = SA[0, 2, 1]
        b_vec = SA[0, 0, 1]
        vparal = 1
        result2 = perpendicular_velocity(vel, b_vec, vparal)
        @test isapprox(result2, [0, 2, 0], atol=1e-6)
    end

    @testset "magneticmoment" begin
        vperp = 1.0e5
        mass = 9.11e-31  # electron mass
        B = 5.0e-5
        result = magneticmoment(vperp, mass, B)
        @test isapprox(result, 0.5 * mass * vperp^2 / B, rtol=1e-6)
    end

    @testset "characteristicfieldlength" begin
        fieldstrength = 5.0e-5
        fieldstrengthgradient = [1.0e-6, 2.0e-6, 3.0e-6]
        result = characteristicfieldlength(fieldstrength, fieldstrengthgradient)
        @test isapprox(result, fieldstrength / norm(fieldstrengthgradient),
            rtol=1e-6)
    end

    @testset "scalesratio" begin
        R = [1, 1, 1]
        ∇B = [0, 2, 0]
        params = (
            charge=-√6,
            mass=9,
            magneticmoment=9,
            fields=(x, y, z, t) -> ([0, 0, 0], [0, 0, 2y + 1]),
        )
        t = 0.0
        result = scalesratio(
            R,
            t,
            params.mass,
            params.charge,
            params.magneticmoment,
            params.fields,
        )
        @test isapprox(result, 2, atol=1e-6)
        # Test different method
        vperp = sqrt(2 * params.magneticmoment * 3 / params.mass)
        result = scalesratio(3, ∇B, vperp, params.charge, params.mass)
        @test isapprox(result, 2, atol=1e-6)
        # Test different method
        vel = [vperp, 0, 1]
        result = scalesratio(
            R, vel, t, params.mass, params.charge,
            params.fields,
        )
        @test isapprox(result, 2, atol=1e-6)
    end
    @testset "magneticcurvatureratio" begin
        R = [1, 1, 1]
        ∇B = [0, 2, 0]
        params = (
            charge=-√6,
            mass=9,
            magneticmoment=9,
            fields=(x, y, z, t) -> ([0, 0, 0], [0, 0, 2y + 1]),
        )
        t = 0.0
        result = magneticcurvatureratio(
            R,
            t,
            params.mass,
            params.charge,
            params.magneticmoment,
            params.fields,
        )
        @test isapprox(result, 0, atol=1e-6)
        # Test different method
        vperp = sqrt(2 * params.magneticmoment * 3 / params.mass)
        vel = [vperp, 0, 1]
        result = magneticcurvatureratio(
            R, vel, t, params.mass, params.charge,
            params.fields,
        )
        @test isapprox(result, 0, atol=1e-6)
    end

    @testset "kineticenergy" begin
        velocity = 1.0e5
        mass = 9.11e-31  # electron mass
        result = kineticenergy(velocity, mass)
        @test isapprox(result, 0.5 * mass * velocity^2, atol=1e-6)
    end

    @testset "exbdrift" begin
        magneticfield = SA[0.0, 0.0, 5.0e-5]
        electricfield = SA[1.0e-3, 0.0, 0.0]
        result = exbdrift(magneticfield, electricfield)
        @test isapprox(
            result,
            (electricfield × (magneticfield / norm(magneticfield))) /
            norm(magneticfield),
            atol=1e-6
        )
    end

    @testset "gradbdrift" begin
        b̂ = SA[0.0, 0.0, 1.0]
        ∇B = SA[1.0e-6, 2.0e-6, 3.0e-6]
        μ = 1.0e-23
        B_inv = 1 / 5.0e-5
        q_inv = 1 / 1.6e-19
        result = gradbdrift(b̂, ∇B, μ, B_inv, q_inv)
        @test isapprox(result, q_inv * B_inv * μ * (b̂ × ∇B), rtol=1e-6)
    end

    @testset "curvaturedrift" begin
        b̂ = SA[0.0, 0.0, 1.0]
        db̂dt = SA[1.0e-6, 2.0e-6, 3.0e-6]
        vparal = 1.0e5
        B_inv = 1 / 5.0e-5
        q_inv = 1 / 1.6e-19
        mass = 9.11e-31  # electron mass
        result = curvaturedrift(b̂, db̂dt, vparal, B_inv, q_inv, mass)
        @test isapprox(result, q_inv * B_inv * mass * b̂ × (vparal * db̂dt), rtol=1e-6)
    end

    @testset "polarisationdrift" begin
        b̂ = SA[0.0, 0.0, 1.0]
        dExBdt = SA[1.0e-6, 2.0e-6, 3.0e-6]
        B_inv = 1 / 5.0e-5
        q_inv = 1 / 1.6e-19
        mass = 9.11e-31  # electron mass
        result = polarisationdrift(b̂, dExBdt, B_inv, q_inv, mass)
        @test isapprox(result, q_inv * B_inv * mass * b̂ × dExBdt, rtol=1e-6)
    end

    @testset "magneticmirror_acceleration" begin
        b̂ = SA[0.0, 0.0, 1.0]
        ∇B = SA[1.0e-6, 2.0e-6, 3.0e-6]
        μ = 1.0e-23
        mass = 9.11e-31  # electron mass
        result = magneticmirror_acceleration(b̂, ∇B, μ, mass)
        @test isapprox(result, -μ * b̂ ⋅ ∇B / mass, rtol=1e-6)
    end

    @testset "parallel_acceleration" begin
        b̂ = SA[0.0, 0.0, 1.0]
        E_vec = SA[1.0e-3, 0.0, 0.0]
        q = 1.6e-19  # proton charge
        mass = 1.67e-27  # proton mass
        result = parallel_acceleration(b̂, E_vec, q, mass)
        @test isapprox(result, q * (E_vec ⋅ b̂) / mass, rtol=1e-6)
    end

    @testset "fermi_acceleration" begin
        ExBdrift = SA[1.0e-3, 0.0, 0.0]
        ∇Bdrift = SA[0.0, 1.0e-3, 0.0]
        dbdt = SA[1.0e-6, 2.0e-6, 3.0e-6]
        result = fermi_acceleration(ExBdrift, ∇Bdrift, dbdt)
        @test isapprox(result, (ExBdrift + ∇Bdrift) ⋅ dbdt, atol=1e-6)
    end

    @testset "fieldgradients" begin
        x, y, z, t = 1.0, 2.0, 3.0, 0.0
        ∇b, ∇ExB, ∇B, B_vec, E_vec, ∂b, ∂ExB, ∂B = fieldgradients(
            x, y, z, t,
            (x, y, z, t) -> ([x / 2, 0, 0], [0, 0, 2y])
        )
        @test isapprox(B_vec, [0.0, 0.0, 4.0], atol=1e-6)
        @test isapprox(E_vec, [0.5, 0.0, 0.0], atol=1e-6)
        @test isapprox(∇B, [0.0, 2.0, 0.0], atol=1e-6)
        @test isapprox(∇b, [0 0 0; 0 0 0; 0 0 0], atol=1e-6)
        @test isapprox(∇ExB, [0 0 0; -1/8 1/16 0; 0 0 0], atol=1e-6)
        @test isapprox(∂b, [0.0, 0.0, 0.0], atol=1e-6)
        @test isapprox(∂ExB, [0.0, 0.0, 0.0], atol=1e-6)
        @test isapprox(∂B, 0.0, atol=1e-6)

        # test the time derivation
        x, y, z, t = 1.0, 2.0, 3.0, 4.0
        ∇b, ∇ExB, ∇B, B_vec, E_vec, ∂b, ∂ExB, ∂B = fieldgradients(
            x, y, z, t,
            (x, y, z, t) -> ([t, 0, 0], [0, 0, 2.0t^2])
        )
        @test isapprox(B_vec, [0.0, 0.0, 32.0], atol=1e-6)
        @test isapprox(E_vec, [4, 0, 0], atol=1e-6)
        @test isapprox(∇B, [0.0, 0, 0.0], atol=1e-6)
        @test isapprox(∇b, [0 0 0; 0 0 0; 0 0 0], atol=1e-6)
        @test isapprox(∇ExB, [0 0 0; 0 0 0; 0 0 0], atol=1e-6)
        @test isapprox(∂b, [0.0, 0.0, 0.0], atol=1e-6)
        @test isapprox(∂ExB, [0.0, 1 / 32, 0.0], atol=1e-6)
        @test isapprox(∂B, 16.0, atol=1e-6)
    end

    @testset "drifts" begin
        x, y, z, t = 1.0, 2.0, 3.0, 0.0
        vparal = 1
        q = 1
        m = 1
        μ = 1
        emfield(x, y, z, t) = ([x / 2, 0, 0], [0, 0, 2y])
        ExBdrift, ∇Bdrift, Rdrift, Pdrift = drifts(
            x, y, z, t, vparal, q, m, μ, emfield)

        v_E = [0, -1 / 8, 0]
        v_∇B = [-μ / 2q, 0, 0]
        v_C = zeros(3)
        v_P = [m / q * 1 / 512, 0, 0]
        @test isapprox(ExBdrift, v_E, atol=1e-6)
        @test isapprox(∇Bdrift, v_∇B, atol=1e-6)
        @test isapprox(Rdrift, v_C, atol=1e-6)
        @test isapprox(Pdrift, v_P, atol=1e-6)
    end

    @testset "gca_drift_and_acceleration" begin
        x, y, z, t = 1.0, 2.0, 3.0, 1.0
        vparal = 1
        q = 1
        m = 1
        μ = 1
        ∇b, ∇ExB, ∇B, B_vec, E_vec, ∂b, ∂ExB, ∂B = fieldgradients(
            x, y, z, t,
            (x, y, z, t) -> ([x * t / 2, 0, 0], [0, 0, 2y * t^2]),
        )
        dellb_addition = [0.1, 0, 0]
        result = gca_drift_and_acceleration(
            ∇b, ∇ExB, ∇B, B_vec, E_vec,
            ∂b + dellb_addition, ∂ExB, vparal,
            q, m, μ
        )
        B = 4.0
        b = [0, 0, 1]
        v_E = [0, -1 / 8, 0]
        v_∇B = [-μ / 2q, 0, 0]
        v_C = 0 .+ (m * vparal / (q * B)) * b × dellb_addition
        v_P = [m / q * 1 / 512, 0, 0] - [m / (q * B) * 0.125, 0, 0]
        vparal_vec = [0, 0, vparal]
        @test isapprox(
            result[1:3],
            vparal_vec .+ v_E .+ v_∇B .+ v_C .+ v_P,
            atol=1e-6
        )
        @test isapprox(
            result[4],
            dot(v_E + v_∇B, dellb_addition),
            atol=1e-6
        )
    end

    @testset "cosineof_pitchangle" begin
        magneticfield = SA[0.0, 0.0, 5.0e-5]
        parallel_velocity = 1.0e5
        mass = 9.11e-31  # electron mass
        magneticmoment = 1.0e-23
        result = cosineof_pitchangle(magneticfield, parallel_velocity, mass, magneticmoment)
        @test isapprox(result, sqrt(1 / (2 * norm(magneticfield) * magneticmoment / (mass * parallel_velocity^2) + 1)), atol=1e-6)
    end

    @testset "lorentzfactor" begin
        speed = 1.0e5
        result = lorentzfactor(speed)
        @test isapprox(result, 1 / sqrt(1 - speed^2 * csqrdinv), rtol=1e-6)
    end

    @testset "kineticspeed" begin
        kineticenergy = 1.0e-13
        mass = 9.11e-31  # electron mass
        result = kineticspeed(kineticenergy, mass)
        @test isapprox(result, sqrt(2 * kineticenergy / mass), rtol=1e-6)
    end


end # testset physics.jl
